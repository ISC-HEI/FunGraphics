plugins {
	id 'java-library'
  id 'scala'
	id "org.jetbrains.kotlin.jvm" version "1.6.20"
	id "com.palantir.git-version" version "0.14.0"
}

def name = 'fungraphics'  // <- Will change the .jar name
def main = 'hevs.graphics.FunGraphics' // <- Main class name

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
}

kotlin {
	jvmToolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
}

test {
	useJUnitPlatform()
	//jvmArgs["-Djava.awt.headless=true"]
	//environment.remove("DISPLAY")
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.scala-lang:scala-library:2.13.9'
  testImplementation 'org.scalatest:scalatest_2.13:3.2.0'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testImplementation 'org.jetbrains.kotlin:kotlin-test'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
	testImplementation 'org.assertj:assertj-core:3.9.2'
	testImplementation 'org.assertj:assertj-swing-junit:3.9.2'
}

def genDir = "$buildDir/gen/"

task versionGen {
	def versionFile = "$genDir/version.txt"
	outputs.upToDateWhen { false }
	outputs.file(versionFile)
	doLast {
		new File(versionFile).text = """${gitVersion()}
"""
	}
}

tasks.named('jar') {
	dependsOn versionGen
	dependsOn test
  dependsOn scaladoc
	manifest {
		attributes('Implementation-Title': name,
				'Implementation-Version': rootProject.version,
				'Main-Class': main)
	}
	archiveBaseName.set(rootProject.name)
	archiveVersion.set(rootProject.version)

	from('src/main/kotlin') {
		include '**/*.kt'
	}
  from('src/main/scala') {
		include '**/*.scala'
    include '**/*.java'
	}
  from(scaladoc.outputs.files) {
    include '**'
    exclude 'hevs/utils/ScalaDocSetup.html'
    exclude 'index.html'
  }

	from("$genDir") {
		rename {
			String filename -> "res/generated/" + filename
		}
	}
}

version = gitVersion()

task runJar(type: JavaExec) {
	dependsOn jar
	mainClass = "-jar";
	args jar.archiveFile.get()
}

task copyJar(type: Copy) {
	from jar // copies output of file produced from jar task
	into "$buildDir"
	rename '.*', "$rootProject.name-dev.jar"
}

javadoc {
    // without the -quiet option, the build fails
    options.addStringOption('Xdoclint:none', '-quiet')
}

build.finalizedBy copyJar
